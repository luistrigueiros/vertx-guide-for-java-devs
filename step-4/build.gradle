buildscript {
  repositories {
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }
  dependencies {
    classpath "gradle.plugin.io.vertx:vertx-gradle-plugin:0.0.8"
  }
}


apply plugin: 'java'

group = 'io.vertx'
version = '1.3.0-SNAPSHOT'

description = """"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
  options.encoding = 'UTF-8'
}



repositories {
  jcenter()
}

ext {
  vertVersion = '3.5.1'
}

dependencies {
  compile "io.vertx:vertx-core:$vertVersion"
  compile "io.vertx:vertx-jdbc-client:$vertVersion"
  compile "io.vertx:vertx-service-proxy:$vertVersion"
  compile "io.vertx:vertx-codegen:$vertVersion"
  compile "io.vertx:vertx-web:$vertVersion"
  compile "io.vertx:vertx-web-client:$vertVersion"
  compile "io.vertx:vertx-web-templ-freemarker:$vertVersion"
  compile "com.github.rjeschke:txtmark:0.13"
  compile "ch.qos.logback:logback-classic:1.2.3"
  compile "org.hsqldb:hsqldb:2.3.4"
  testCompile 'junit:junit:4.12'
  testCompile "io.vertx:vertx-unit:$vertVersion"
}


sourceSets {
  generated {
    java.srcDir "${projectDir}/src/generated/java"
  }
}

task generateProxies(type: JavaCompile) {
  group = "build"
  description = "Generate Vert.x service proxies"

  source = sourceSets.main.java
  classpath = configurations.compile
  options.compilerArgs = [
    "-proc:only",
    "-processor", "io.vertx.codegen.CodeGenProcessor",
    "-Acodegen.output=${projectDir}/src/main"
  ]
  destinationDir = file("${projectDir}/src/generated/java")
}

compileJava {
  dependsOn generateProxies
  source += sourceSets.generated.java
}

clean {
  delete += sourceSets.generated.java.srcDirs
}
